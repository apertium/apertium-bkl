Alphabet
 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z
 a b c d e f g h i j k l m n o p q r s t u v w x y z
 %{F%}:f %{F%}:b
 %{S%}:s %{S%}:m
 %{I%}:0 ! %{I%}:i
 %{A%}:a %{A%}:0
 %{W%}:w ! %{W%}:0
 %{O%}:o %{O%}:u
 %{l%}:l %{l%}:0
 %{E%}:e %{E%}:a
 %{N%}:n ! %{N%}:l
 %{V%}:e ! %{V%}:i %{V%}:a %{V%}:o
 %{e%}:e ! %{e%}:0
 %{a%}:a ! %{a%}:0
 %>:0  ;

Sets

Vowel = A E I O U
        a e i o u ;
        
VerbSkipCons = f l s n ;

Rules

"n becomes l in i_i in 'give'"
%{N%}:l <=> :i _ :i ;

"in 'give', o only shows up at the end"
%{V%}:o <=> :t :o :0 :b :a :f _ .#. ;

"in 'give', a only shows up at the end if the previous vowel was a"
%{V%}:a <=> :a :VerbSkipCons _ ;

"in 'give', i only shows up at the end if the previous vowel was i"
%{V%}:i <=> :i :VerbSkipCons _ ;

"1st person pronoun only has i with no suffixes"
%{I%}:i <=> _ .#. ;
            _ y ;
            _ s ;

"e in disappears in nouns suffixes after vowels"
%{e%}:0 <=> :Vowel _ ;
"a in disappears in nouns suffixes after vowels"
%{a%}:0 <=> :Vowel _ ;
"w in disappears in nouns suffixes after nasals"
%{W%}:0 <=> :m _ ;
            :n _ ;
            :n :g _ ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst

