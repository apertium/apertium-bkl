Alphabet
 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z
 a b c d e f g h i j k l m n o p q r s t u v w x y z
 %{F%}:f %{F%}:b ! no known pattern for these 2
 %{S%}:s %{S%}:m ! grammatical sketch says maybe free variation
 %{I%}:0 ! %{I%}:i
 %{W%}:w ! %{W%}:0
 %{O%}:o ! %{O%}:u
 %{l%}:0 ! %{l%}:l
 %{E%}:a ! %{E%}:e
 %{N%}:n ! %{N%}:l
 %{V%}:e ! %{V%}:a %{V%}:i
 %{e%}:e ! %{e%}:0
 %{a%}:a ! %{a%}:0
 %>:0  ;

Sets

Vowel = A E I O U
        a e i o u ;
        
VerbSkipCons = f l s n ;

Rules

"{l} always shows up after u and in o_bif"
%{l%}:l <=> :u _ ;
            :o _ :b :i :f ;
            :o _ :b :i :l :i :n ;
            
"{O} is u in some places"
%{O%}:u <=> :g _ %{l%}: b :a ;
            :t _ %{l%}: b :a :VerbSkipCons :a :n ;
            :t _ %{l%}: b :i :f ;
            :t _ %{l%}: b :i :l :i :n ;

"n becomes l in i_i in 'give'"
%{N%}:l <=> :i _ :i ;

"in 'give', a only shows up at the end if the previous vowel was a"
%{V%}:a <=> :a :VerbSkipCons _ ;
            :r :e :VerbSkipCons _ ; ! or after re in 'tie'

"in 'give', i only shows up at the end if the previous vowel was i"
%{V%}:i <=> :i :VerbSkipCons _ ;
            
"masculine shows up as e in 'give' only after rb"
%{E%}:e <=> :r :b _ ;

"1st person pronoun only has i with no suffixes"
%{I%}:i <=> _ .#. ;
            _ y ;
            _ s ;

"e in disappears in nouns suffixes after vowels"
%{e%}:0 <=> :Vowel _ ;
"a in disappears in nouns suffixes after vowels"
%{a%}:0 <=> :Vowel _ ;
"w in disappears in nouns suffixes after nasals"
%{W%}:0 <=> :m _ ;
            :n _ ;
            :n :g _ ;

! Resources:
! https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! https://wiki.apertium.org/wiki/Twol
! https://wiki.apertium.org/wiki/Hfst

