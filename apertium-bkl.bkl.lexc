! Morphological Transducer for bkl

Multichar_Symbols

! Part of speech categories
%<n%>     ! Noun
%<neg%>   ! Negation
%<resp%>  ! Response
%<psimp%> ! Pseudo-Imperatives
%<ij%>    ! Interjection
%<itg%>   ! Interogative
%<num%>   ! Numeral
%<adj%>   ! Adjective
%<adv%>   ! Adverb
%<man%>   ! Manner (subclass of Adverb)
%<temp%>  ! Temporal
%<locl%>  ! Locational
%<conj%>  ! Conjunction
%<post%>  ! Postposition
%<v%>     ! Verb
%<tv%>    ! Transitive Verb
%<iv%>    ! Intransitive Verb

! Number morphology
%<pl%>  ! Plural
%<sg%>  ! Singular
%<du%>  ! Dual

! Cases
%<subj%>
%<ins%>
%<pos%>
%<com%>
%<dat%>
%<acc%>
%<relational%>
%<tv%>

! Tense
%<prs%>
%<pst%>
%<fut%>

! Pronouns
%<ref%>   ! Reflexive
%<dem%>   ! Demonstrative
%<part%>  ! Particular

! Modifiers
%<int%>  ! Intensifier
%<advl%> ! Adverbial

! Verb Morphology
%<lg%>
%<m%>
%<f%>
%<light%>   ! Action during the day (verbs)
%<dark%>  ! Action at night
%<low%>   ! Action down low
%<high%>  ! Action up high
%<dst%>   ! Action far away

! Other symbols
%>      ! Morpheme boundary
%{F%}   ! for commitative
%{S%}   ! for pronoun accusative
%{I%}   ! for variation in 1S pronoun
%{A%}   ! for postpositions
%{W%}   ! for postposition -wer (w deleted after nasals)
%{O%}
%{l%}
%{E%}
%{N%}
%{V%}

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NounRoot ;
ClosedClass ;
PronounRoot ;
ModifierRoot ;
VerbRoot ;
GiveInflNum ;

LEXICON RegNounInfl

! noun ends in vowel -> drop initial vowel of suffix (TODO)

%<n%>%<subj%>:na # ;
%<n%>+yan%<neg%>:yan # ;
%<n%>%<ins%>:em # ;
%<n%>%<relational%>:bara # ;
%<n%>%<pos%>:em # ;
%<n%>%<pos%>:mana # ;
%<n%>%<com%>:%{F%}ar # ;
%<n%>%<dat%>:f # ;
%<n%>+ap%<post%>:%{A%}p # ;
%<n%>+wer%<post%>:%{W%}er # ;
%<n%>%<acc%>:s # ;
%<n%>:0 # ; ! grammar sketch says "However, not all nouns as Subject take this inflection.", assuming this means they take nothing

LEXICON PronounInfl

%<acc%>:%{S%} # ;
%<dat%>:p # ;
%<com%>:bar # ;
%<acc%>%<ref%>:nan # ;
+yan%<neg%>:yan # ;
%<dem%>%<part%>:serem # ;
# ;

! -mena/-rem unclear (TODO)

LEXICON ModifierInfl

%<adj%>:0 # ;
%<adj%>%<int%>:sus # ;
%<adj%>%<advl%>:%{F%}er # ;
%<adj%>+yan%<neg%>:yan # ;
%<adj%>:tena VerbInfl ;

! it's not explicit, but these suffixes probably apply to both adverbs and adjectives (TODO)
! double intensification is marked with full reduplication (waaken -> waaken-waakensus) (TODO)

LEXICON VerbInfl

# ;


LEXICON GiveInflNum
! currently only works for give, and only for the forms listed in the table in the grammar sketch
! could maybe be generalized (TODO)

gubana%<v%>%<tv%>%<sg%>:g%{O%}%{l%} GiveInflSize ;
gubana%<v%>%<tv%>%<du%>:ter GiveInflSize ;
gubana%<v%>%<tv%>%<pl%>:kit%{O%}%{l%} GiveInflSize ;

LEXICON GiveInflSize

%<lg%>:b GiveInflGender ;

LEXICON GiveInflGender

%<m%>:%{E%} GiveInflTense ;
%<f%>:i GiveInflTense ;

LEXICON GiveInflTense

%<pst%>%<dark%>:f%{V%}nt # ;
%<pst%>%<light%>:%{N%}%{V%}nt # ;
%<prs%>%<dark%>:s%{V%} # ;
%<prs%>%<light%>:%{N%}%{V%} # ;
%<fut%>:f%{V%} # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounRoot

o:o RegNounInfl ; ! "earth, soil"
son:son RegNounInfl ; ! "dust, ashes"
betik:betik RegNounInfl ; ! "mud"
dwis:dwis RegNounInfl ; ! "sand"
ton:ton RegNounInfl ; ! "stone"
bangge:bangge RegNounInfl ; ! "squirrel-fox"
fo:fo RegNounInfl ; ! "water"
jina:jina RegNounInfl ; ! "house"
sita:sita RegNounInfl ; ! "outside"
tane:tane RegNounInfl ; ! "child"
bwernabar:bwernabar RegNounInfl ; ! "sickness"
at:at RegNounInfl ; ! "canoe"
gin:gin RegNounInfl ; ! "arrow"
uwa:uwa RegNounInfl ; ! "father"
Korano:Korano RegNounInfl ; ! "chief"
yafontoi:yafontoi RegNounInfl ; ! "daughter"
gwora:gwora RegNounInfl ; ! "cover"
Mina:Mina RegNounInfl ; ! "(name)"
Suster:Suster RegNounInfl ; ! "Sister"
nan:nan RegNounInfl ; ! "who"

LEXICON ClosedClass

ngga%<resp%>:ngga # ; ! "yes"
sia%<resp%>:sia # ; ! "yes"
wowo%<resp%>:wowo # ; ! "no"
fas%<resp%>:fas # ; ! "none"
bar%<resp%>:bar # ; ! "finished"
bai%<resp%>:bai # ; ! "don't want"
au%<psimp%>:au # ; ! "come"
aiyai%<psimp%>:aiyai # ; ! "watch out"
jesbaf%<psimp%>:jesbaf # ; ! "don't"
wa%<ij%>:wa # ; ! "(amazement)"
nesik%<ij%>:nesik # ; ! "(pity)"
basa%<itg%>:basa # ; ! "what"
nasa%<itg%>:nasa # ; ! "who"
bafa%<itg%>:bafa # ; ! "why"
fomera%<itg%>:fomera # ; ! "how many"
fwera%<itg%>:fwera # ; ! "where"
fonggalabar%<itg%>:fonggalabar # ; ! "when"
daamfena%<num%>:daamfena # ; ! "one"
naura%<num%>:naura # ; ! "two"
nawerningna%<num%>:nawerningna # ; ! "three"
nawernaura%<num%>:nawernaura # ; ! "four"
mese%<adv%>%<man%>:mese # ; ! "also"
gamjon%<adv%>%<man%>:gamjon # ; ! "again"
enggam%<adv%>%<man%>:enggam # ; ! "like this"
maa%<adv%>%<man%>:maa # ; ! "already"
gamerje%<adv%>%<man%>:gamerje # ; ! "not yet"
galap%<adv%>%<man%>:galap # ; ! "later"
namwer%<temp%>:namwer # ; ! "today, now"
gwirmir%<temp%>:gwirmir # ; ! "tomorrow"
ir%<temp%>:ir # ; ! "yesterday"
jem%<temp%>:jem # ; ! "day before or day after"
gwerem%<temp%>:gwerem # ; ! "noon"
dafef%<temp%>:dafef # ; ! "afternoon"
afunup%<locl%>:afunup # ; ! "middle of"
safap%<locl%>:safap # ; ! "on"
burawer%<locl%>:burawer # ; ! "behind"
yo%<conj%>:yo # ; ! "and"
afa%<conj%>:afa # ; ! "or"
ane%<conj%>:ane # ; ! "and"
ga%<conj%>:ga # ; ! "and"
jeuga%<conj%>:jeuga # ; ! "but"
jebas%<conj%>:jebas # ; ! "because"
jengga%<conj%>:jengga # ; ! "then"
jamer%<conj%>:jamer # ; ! "until"

LEXICON PronounRoot

! pronouns
ai%<prn%>:a%{I%} PronounInfl ; ! "I"
ne%<prn%>:ne PronounInfl ; ! "we"
i%<prn%>:i PronounInfl ; ! "you (SG)"
je%<prn%>:je PronounInfl ; ! "he, she, it"

! subject 1
ai%<prn%>%<subj%>:ai PronounInfl ; ! "I (subj)"
i%<prn%>%<subj%>:aame PronounInfl ; ! "you (SG, subj)"
je%<prn%>%<subj%>:je PronounInfl ; ! "he, she, it (subj)"
ne%<prn%>%<subj%>:ne PronounInfl ; ! "we (subj)"

! subject 2
ai%<prn%>%<subj%>:ajam PronounInfl ; ! "I (subj)"
i%<prn%>%<subj%>:ijam PronounInfl ; ! "you (SG, subj)"
jam%<prn%>%<subj%>:je PronounInfl ; ! "he, she, it (subj)"
ne%<prn%>%<subj%>:nejam PronounInfl ; ! "we (subj)"
! it might actually be "ne jam" (TODO)

LEXICON ModifierRoot

ane:ane ModifierInfl ; ! "many"
seyafter:seyafter ModifierInfl ; ! "all"
fas:fas ModifierInfl ; ! "none"
waakena:waaken%{A%} ModifierInfl ; ! "good"
sasara:sasara ModifierInfl ; ! "happy"
baabeta:baabeta ModifierInfl ; ! "strong"
bunar:bunar ModifierInfl ; ! "true"
samem:samem ModifierInfl ; ! "slow"
taban:taban ModifierInfl ; ! "finished"
giri:giri ModifierInfl ; ! "deep, tall"
gwetmana:gwetmana ModifierInfl ; ! "near"
bijua:bijua ModifierInfl ; ! "far"
teten:teten ModifierInfl ; ! "empty"
enbisini:enbisini ModifierInfl ; ! "full"
kakala:kakala ModifierInfl ; ! "hot"
wisimi:wisimi ModifierInfl ; ! "cold"
kekelna:kekelna ModifierInfl ; ! "hard"
nonona:nonona ModifierInfl ; ! "soft"
titini:titini ModifierInfl ; ! "wet"
setera:setera ModifierInfl ; ! "dry"
unggwandusa:unggwandusa ModifierInfl ; ! "large"
bastantoiya:bastantoiya ModifierInfl ; ! "small" has irregular <advl> form, generates regular also (TODO)
bastantoiya%<adj%>%<advl%>:bastantoifer # ; ! "a little (adv)"
bukona:bukona ModifierInfl ; ! "round"
ferfera:ferfera ModifierInfl ; ! "flat"
bubwalna:bubwalna ModifierInfl ; ! "long"
tofora:tofora ModifierInfl ; ! "short"
sinsini:sinsini ModifierInfl ; ! "white"
seseye:seseye ModifierInfl ; ! "black"
berbere:berbere ModifierInfl ; ! "red"
ikikini:ikikini ModifierInfl ; ! "green"
bwelkat-bwelkata:bwelkat-bwelkata ModifierInfl ; ! "yellow"
ibam-ibama:ibam-ibama ModifierInfl ; ! "blue"

LEXICON VerbRoot

gubana%<v%>%<tv%>%<prs%>%<sg%>%<light%>:gulbana VerbInfl ;
gubana%<v%>%<tv%>%<pst%>%<sg%>%<light%>:gulbanant VerbInfl ;
gubana%<v%>%<tv%>%<fut%>%<sg%>%<light%>:gulbafa VerbInfl ;
gubana%<v%>%<tv%>%<prs%>%<sg%>%<dark%>:gulbasa VerbInfl ;
gubana%<v%>%<tv%>%<pst%>%<sg%>%<dark%>:gulbafant VerbInfl ;
gubana%<v%>%<tv%>%<fut%>%<sg%>%<dark%>:gubafa VerbInfl ;
gubana%<v%>%<tv%>%<prs%>%<du%>%<light%>:terbene VerbInfl ;
gubana%<v%>%<tv%>%<pst%>%<du%>%<light%>:terbenent VerbInfl ;
gubana%<v%>%<tv%>%<fut%>%<du%>%<light%>:terbefe VerbInfl ;
gubana%<v%>%<tv%>%<prs%>%<du%>%<dark%>:terbese VerbInfl ;
gubana%<v%>%<tv%>%<pst%>%<du%>%<dark%>:terbefent VerbInfl ;
gubana%<v%>%<tv%>%<fut%>%<du%>%<dark%>:terbefe VerbInfl ;
gubana%<v%>%<tv%>%<prs%>%<pl%>%<light%>:kitobana VerbInfl ;
gubana%<v%>%<tv%>%<pst%>%<pl%>%<light%>:kitulbanant VerbInfl ;
gubana%<v%>%<tv%>%<fut%>%<pl%>%<light%>:kitobafo VerbInfl ;
gubana%<v%>%<tv%>%<prs%>%<pl%>%<dark%>:kitobasa VerbInfl ;
gubana%<v%>%<tv%>%<pst%>%<pl%>%<dark%>:kitulbafant VerbInfl ;
gubana%<v%>%<tv%>%<fut%>%<pl%>%<dark%>:kitobafo VerbInfl ;

gobali%<v%>%<tv%>%<prs%>%<sg%>%<light%>:gobili VerbInfl ;
gobali%<v%>%<tv%>%<pst%>%<sg%>%<light%>:golbilint VerbInfl ;
gobali%<v%>%<tv%>%<fut%>%<sg%>%<light%>:golbifi VerbInfl ;
gobali%<v%>%<tv%>%<prs%>%<sg%>%<dark%>:gobisi VerbInfl ;
gobali%<v%>%<tv%>%<pst%>%<sg%>%<dark%>:golbifint VerbInfl ;
gobali%<v%>%<tv%>%<fut%>%<sg%>%<dark%>:golbifi VerbInfl ;
gobali%<v%>%<tv%>%<prs%>%<du%>%<light%>:terbili VerbInfl ;
gobali%<v%>%<tv%>%<pst%>%<du%>%<light%>:terbilint VerbInfl ;
gobali%<v%>%<tv%>%<fut%>%<du%>%<light%>:terbifi VerbInfl ;
gobali%<v%>%<tv%>%<prs%>%<du%>%<dark%>:terbese VerbInfl ;
gobali%<v%>%<tv%>%<pst%>%<du%>%<dark%>:terbefint VerbInfl ;
gobali%<v%>%<tv%>%<fut%>%<du%>%<dark%>:terbifi VerbInfl ;
gobali%<v%>%<tv%>%<prs%>%<pl%>%<light%>:kitobili VerbInfl ;
gobali%<v%>%<tv%>%<pst%>%<pl%>%<light%>:kitulbilint VerbInfl ;
gobali%<v%>%<tv%>%<fut%>%<pl%>%<light%>:kitulbifi VerbInfl ;
gobali%<v%>%<tv%>%<prs%>%<pl%>%<dark%>:kitobisi VerbInfl ;
gobali%<v%>%<tv%>%<pst%>%<pl%>%<dark%>:kitulbifint VerbInfl ;
gobali%<v%>%<tv%>%<fut%>%<pl%>%<dark%>:kitulbifi VerbInfl ;

fwirna%<v%>%<tv%>%<prs%>%<sg%>%<light%>:fwirena VerbInfl ;
fwirna%<v%>%<tv%>%<prs%>%<sg%>%<dark%>:fwiresa VerbInfl ;
fwirna%<v%>%<tv%>%<prs%>%<du%>%<light%>:faarena VerbInfl ;
fwirna%<v%>%<tv%>%<prs%>%<du%>%<dark%>:faaresa VerbInfl ;
fwirna%<v%>%<tv%>%<prs%>%<pl%>%<light%>:taferebili VerbInfl ;
fwirna%<v%>%<tv%>%<prs%>%<pl%>%<dark%>:taferebisi VerbInfl ;
fwirna%<v%>%<tv%>%<pst%>%<sg%>%<light%>:fwirenant VerbInfl ;
fwirna%<v%>%<tv%>%<pst%>%<sg%>%<dark%>:fwirenant VerbInfl ;
fwirna%<v%>%<tv%>%<pst%>%<du%>%<light%>:faarefant VerbInfl ;
fwirna%<v%>%<tv%>%<pst%>%<du%>%<dark%>:faarenant VerbInfl ;
fwirna%<v%>%<tv%>%<pst%>%<pl%>%<light%>:taferebilint VerbInfl ;
fwirna%<v%>%<tv%>%<pst%>%<pl%>%<dark%>:taferebifint VerbInfl ;
fwirna%<v%>%<tv%>%<fut%>%<sg%>%<light%>:fwirena VerbInfl ;
fwirna%<v%>%<tv%>%<fut%>%<sg%>%<dark%>:fwiresa VerbInfl ;
fwirna%<v%>%<tv%>%<fut%>%<du%>%<light%>:faarefa VerbInfl ;
fwirna%<v%>%<tv%>%<fut%>%<du%>%<dark%>:faaresa VerbInfl ;
fwirna%<v%>%<tv%>%<fut%>%<pl%>%<light%>:taferebifi VerbInfl ;
fwirna%<v%>%<tv%>%<fut%>%<pl%>%<dark%>:taferebisi VerbInfl ;

gwarantena%<v%>%<tv%>%<prs%>%<sg%>%<low%>:gwerantena VerbInfl ;
gwarantena%<v%>%<tv%>%<prs%>%<sg%>%<high%>:gweransona VerbInfl ;
gwarantena%<v%>%<tv%>%<prs%>%<du%>%<low%>:tosontona VerbInfl ;
gwarantena%<v%>%<tv%>%<prs%>%<du%>%<high%>:tosonsona VerbInfl ;
gwarantena%<v%>%<tv%>%<prs%>%<pl%>%<low%>:bununtona VerbInfl ;
gwarantena%<v%>%<tv%>%<prs%>%<pl%>%<high%>:bununsona VerbInfl ;
gwarantena%<v%>%<tv%>%<prs%>%<dst%>%<sg%>%<low%>:gwerantetfa VerbInfl ;
gwarantena%<v%>%<tv%>%<prs%>%<dst%>%<sg%>%<high%>:gweranswetna VerbInfl ;
gwarantena%<v%>%<tv%>%<prs%>%<dst%>%<du%>%<low%>:tosontetfa VerbInfl ;
gwarantena%<v%>%<tv%>%<prs%>%<dst%>%<du%>%<high%>:tosonswetna VerbInfl ;
gwarantena%<v%>%<tv%>%<prs%>%<dst%>%<pl%>%<low%>:bununtetfa VerbInfl ;
gwarantena%<v%>%<tv%>%<prs%>%<dst%>%<pl%>%<high%>:bununswetna VerbInfl ;


! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! http://wiki.apertium.org/wiki/Hfst

